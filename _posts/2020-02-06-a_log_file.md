---
layout: post
title:  "A Log File"
date:   2020-02-06 01:23:45 +1000
categories: PeopleCode AppEngine Logging
blurb: Extending the File API Object.

---
A nice little extension of the ```File``` API Object to use it as a log file. 
Can be disabled. Detects and handles the Application Engine running on the client.

```
class File extends File
   property boolean isDisabled;
   method File(&name_ As string, &processName_ As string);
   method writeLine(&line_ As string);
private
   instance boolean &_haveWrittenDisabledMessage;
   method writeDisabledMessage();
   method isRunningOnServer(&processName As string) Returns boolean;
end-class;

method File
   /+ &name_ as String, +/
   /+ &processName_ as String +/
   If %This.isRunningOnServer(&processName_) Then
      %Super = GetFile(&name_, "W");
   Else
      %Super = GetFile("C:\temp\\" | &name_, "W", %FilePath_Absolute);
   End-If;
end-method;

method writeLine
   /+ &line_ as String +/
   /+ Extends/implements File.WriteLine +/
   If %This.isDisabled Then
      %This.writeDisabledMessage();
      Return;
   End-If;
   %Super.WriteLine(&line_);
end-method;

method writeDisabledMessage
   If &_haveWrittenDisabledMessage Then
      Return;
   End-If;
   %Super.WriteLine("Logging has been disabled.");
   &_haveWrittenDisabledMessage = True;
end-method;

method isRunningOnServer
   /+ &processName as String +/
   /+ Returns Boolean +/
   Local string &serverName;
   SQLExec("select servernamerqst from psprcsrqst where prcsname = :1", &processName, &serverName);
   Return All(&serverName);
end-method;
```
